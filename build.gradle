import java.nio.file.Paths

plugins {
	// Add Java compilation, testing and bundling capabilities.
	id 'java'

	// Apply the application plugin to add support for building a CLI application.
	id 'application'

	// https://docs.gradle.org/current/userguide/idea_plugin.html
	id 'idea'

	// https://plugins.gradle.org/plugin/com.github.johnrengelman.shadow
	id "com.github.johnrengelman.shadow" version "6.1.0"
}
apply from: 'setup.gradle'

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(8))
		vendor = JvmVendorSpec.ADOPTOPENJDK
	}
}

idea {
	module {
		inheritOutputDirs = false
		outputDir = file('build/production/classes')
		testOutputDir = file('build/test/classes')
	}
}

repositories {
	mavenCentral()
}

configurations {
	implementation.extendsFrom(zomboidImplementation)
	runtimeOnly.extendsFrom(zomboidRuntimeOnly)
}

dependencies {
	// https://mvnrepository.com/artifact/org.ow2.asm/asm
	implementation group: 'org.ow2.asm', name: 'asm', version: '9.0'

	// https://mvnrepository.com/artifact/org.ow2.asm/asm-tree
	implementation group: 'org.ow2.asm', name: 'asm-tree', version: '9.0'

	// https://mvnrepository.com/artifact/org.ow2.asm/asm-util
	implementation group: 'org.ow2.asm', name: 'asm-util', version: '9.0'

	// https://mvnrepository.com/artifact/org.ow2.asm/asm-analysis
	implementation group: 'org.ow2.asm', name: 'asm-analysis', version: '9.0'

	// https://mvnrepository.com/artifact/io.github.java-diff-utils/java-diff-utils
	implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: '4.9'

	// Project Zomboid classes
	if (project.ext.has('gameDir')) {
		zomboidImplementation files(zomboidClassesDir)
	}
	// Project Zomboid libraries
	zomboidRuntimeOnly fileTree(dir: gameDir, include: ['*.jar'])

	// tools.jar is not on JDK classpath by default
	if (project.ext.has('jdkDir'))
	{
		def toolsJar = Paths.get(jdkDir, 'lib', 'tools.jar')
		if (toolsJar.toFile().exists()) {
			implementation files(toolsJar)
		}
		else logger.error("Unable to find tools.jar in JDK directory (${toolsJar.toString()})")
	}
}

application {
	mainClassName = 'dev.weary.zomboid.Main'
}

shadowJar {
	archiveClassifier.set('agent')
	manifest {
		attributes(
				'Can-Redefine-Classes'		: 'true',
				'Can-Retransform-Classes'	: 'true',
				'Agent-Class'				: 'dev.weary.zomboid.agent.Agent',
				'Premain-Class'				: 'dev.weary.zomboid.agent.Agent'
		)
	}
}

